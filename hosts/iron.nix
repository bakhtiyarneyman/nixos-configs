# Do not modify this file!  It was generated by ‘nixos-generate-config’
# and may be overwritten by future invocations.  Please make changes
# to /etc/nixos/configuration.nix instead.
{ config, lib, pkgs, boot, ... }:
let
  diskIds = [
    "nvme-WD_BLACK_SN770_1TB_23085A802755"
    "nvme-WD_BLACK_SN770_1TB_23100L801126"
  ];
  toPartitionId = diskId: partition: "${diskId}-part${toString partition}";
  toDevice = id: "/dev/disk/by-id/${id}";

  inherit (builtins) head toString map tail foldl';
in
{
  programs.i3status-rust = {
    networkInterface = "eno1";
    batteries = [
      {
        device = "battery_hidpp_battery_0";
        name = "";
      }
      {
        device = "battery_hidpp_battery_1";
        name = "";
      }
      {
        device = "ups_hiddev1";
        name = "";
      }
    ];
  };

  boot = {
    extraModulePackages = [ config.boot.kernelPackages.rtl88x2bu ];
    initrd = {
      availableKernelModules = [ "xhci_pci" "ehci_pci" "nvme" "ahci" "usb_storage" "usbhid" "sd_mod" ];
      luks.devices =
        let
          insertDevice = devices: diskId:
            let partitionId = toPartitionId diskId 3;
            in
            devices // {
              ${"decrypted-${partitionId}"} = { device = toDevice partitionId; };
            };
        in
        foldl' insertDevice { } diskIds;
    };
    kernelModules = [ "zfs" ];
    kernelPackages = config.boot.zfs.package.latestCompatibleLinuxPackages;
    loader = {
      efi.efiSysMountPoint = "/boot/efis/${toPartitionId (head diskIds) 1}";
      grub = {
        enable = true;
        version = 2;
        devices = map toDevice diskIds;
        efiSupport = true;
        extraInstallCommands = (toString (map
          (diskId: ''
            set -x
            ${pkgs.coreutils-full}/bin/cp -r \
              ${config.boot.loader.efi.efiSysMountPoint}/EFI \
              /boot/efis/${toPartitionId diskId 1}
            set +x
          '')
          (tail diskIds)));
        zfsSupport = true;
      };
    };
    supportedFilesystems = [ "zfs" ];
    zfs.forceImportRoot = false;
  };

  swapDevices =
    let
      toSwapDevice = diskId:
        let
          partitionId = toPartitionId diskId 4;
          device = toDevice partitionId;
        in
        {
          device = "/dev/mapper/decrypted-${partitionId}";
          encrypted = {
            blkDev = device;
            enable = true;
            # Created with `dd count=1 bs=512 if=/dev/urandom of=/etc/swap.key`.
            keyFile = "/mnt-root/etc/swap.key";
            label = "decrypted-${partitionId}";
          };
        };
    in
    map toSwapDevice diskIds;

  fileSystems =
    let
      fss =
        {
          "/boot" = {
            device = "boot/nixos/root";
            fsType = "zfs";
          };
          "/" = {
            device = "main/nixos/root";
            fsType = "zfs";
          };
          "/home" = {
            device = "main/nixos/home";
            fsType = "zfs";
          };
          "/var" = {
            device = "main/nixos/var";
            fsType = "zfs";
          };
          "/var/lib" = {
            device = "main/nixos/var/lib";
            fsType = "zfs";
          };
          "/var/log" = {
            device = "main/nixos/var/log";
            fsType = "zfs";
          };
          "/mnt/storage" = {
            device = "/dev/disk/by-uuid/d838985e-9b04-4d7f-84c7-de7b73186858";
            fsType = "btrfs";
          };
        };
      insertBootFilesystem = fss: diskId:
        let
          partitionId = toPartitionId diskId 1;
        in
        fss // {
          "/boot/efis/${partitionId}" = {
            device = toDevice partitionId;
            fsType = "vfat";
            options = [
              "x-systemd.idle-timeout=1min"
              "x-systemd.automount"
              "noauto"
              "nofail"
              "noatime"
              "X-mount.mkdir"
            ];
          };
        };
    in
    foldl' insertBootFilesystem fss diskIds;

  networking.hostId = "a7a93500";
  # This value determines the NixOS release with which your system is to be
  # compatible, in order to avoid breaking some software such as database
  # servers. You should change this only after NixOS release notes say you
  # should.
  system.stateVersion = "22.11";
  services = {
    cron = {
      enable = true;
      mailto = let at = "@"; in "bakhtiyarneyman${at}gmail.com";
      systemCronJobs = [
        "0 20 * * Sun bakhtiyar rsync --archive --verbose OneDrive/mezar/ /mnt/storage/backups/mezar/(date +%Y%m%d%H%M%S)"
      ];
    };

    xserver = {
      videoDrivers = [ "amdgpu" ];
      xrandrHeads = [
        { output = "DP-1"; primary = true; }
        { output = "DP-3"; }
      ];
      dpi = 175;
      displayManager = {
        gdm.enable = true;
        setupCommands = ''
          ${pkgs.xorg.xrandr}/bin/xrandr --setprovideroutputsource "modesetting" NVIDIA-0
          ${pkgs.xorg.xrandr}/bin/xrandr --output DP-1 --auto --primary --output DP-3 --auto --right-of DP-1
        '';
      };
    };
  };

  programs.sway = {
    extraOptions = [ "--unsupported-gpu" ];
    extraSessionCommands = ''
      export WLR_NO_HARDWARE_CURSORS=1
    '';
  };
  virtualisation.docker.enableNvidia = true;
}
