#!/usr/bin/env fish

set sock "$XDG_RUNTIME_DIR/claude-notify.sock"

# Walk up the process tree to find which Alacritty terminal we're running in.
set terminal_pid ""
set ancestor $fish_pid
while test "$ancestor" -gt 1
    set name (cat /proc/$ancestor/comm 2>/dev/null)
    if test "$name" = alacritty
        set terminal_pid $ancestor
        break
    end
    set ancestor (awk '/PPid/{print $2}' /proc/$ancestor/status 2>/dev/null)
end

# If that terminal is focused, let the terminal UI handle it.
set focused_pid (swaymsg -t get_tree 2>/dev/null | jq '.. | select(.focused? == true) | .pid')
if test "$focused_pid" = "$terminal_pid" -a -n "$terminal_pid"
    exit 0
end

# Read hook input from stdin.
set input (cat)

set tool (echo $input | jq -r '.tool_name')
set detail (echo $input | jq -r '.tool_input | to_entries | map(.key + ": " + (.value | tostring)) | join(", ")' | cut -c1-200)
set body "$tool: $detail"

# Send via socket (SSH) or notify-send (local).
if test -S "$sock" -a -z "$terminal_pid"
    set response (jq -n --arg body "$body" '{type:"permission",body:$body}' | socat - UNIX-CONNECT:$sock)
    set action (echo $response | jq -r '.action')
else
    set action (notify-send -A allow=Allow -A deny=Deny -A default=Focus 'Claude Code' "$body")
end

if test "$action" = allow
    echo '{"hookSpecificOutput":{"hookEventName":"PermissionRequest","decision":{"behavior":"allow"}}}'
else if test "$action" = deny
    echo '{"hookSpecificOutput":{"hookEventName":"PermissionRequest","decision":{"behavior":"deny","message":"Rejected via notification"}}}'
end

# Focus terminal when user explicitly clicks Deny or Focus.
if test -n "$terminal_pid" -a \( "$action" = deny -o "$action" = default \)
    # swaymsg outputs JSON to stdout (e.g. [{"success":true}]), which would
    # corrupt the hook's JSON output that Claude Code parses. Redirect to /dev/null.
    swaymsg "[pid=$terminal_pid] focus" >/dev/null
end
